buildscript {
  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:3.6.1"
  }
}

apply plugin: "com.android.application"

repositories {
  mavenCentral()
  google()
}

def twaManifest = [
    applicationId: "io.thecoders.safesafe",
    hostName: "safesafe.thecoders.io",
    launchUrl: "/",
    name: "SafeSafe",
    themeColor: "#303f9f",
    navigationColor: "#303f9f",
    backgroundColor: "#bababa",
    enableNotifications: false,
]

android {
  compileSdkVersion 29

  defaultConfig {
    applicationId twaManifest.applicationId
    minSdkVersion 23
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"

    proguardFiles "shrink-rules.pro"

    resValue "string", "appName", twaManifest.name

    def launchUrl = "https://${twaManifest.hostName}${twaManifest.launchUrl}"
    resValue "string", "launchUrl", launchUrl

    resValue "string", "crosLaunchUrl", launchUrl

    resValue "string", "hostName", twaManifest.hostName

    resValue "string", "assetStatements", """
            [{
                "relation": ["delegate_permission/common.handle_all_urls"],
                "target": {
                    "namespace": "web",
                    "site": "https://$twaManifest.hostName"
                }
            }]"""

    resValue "color", "colorPrimary", twaManifest.themeColor

    resValue "color", "navigationColor", twaManifest.navigationColor

    resValue "color", "backgroundColor", twaManifest.backgroundColor

    resValue "bool", "enableNotification", "${twaManifest.enableNotifications}"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  def uploadKeystore = file("upload.keystore")
  def keystoreProperties = file("upload-keystore.properties")
  def useUploadKeystore = uploadKeystore.exists() && keystoreProperties.exists()

  signingConfigs {
    debug {
      keyAlias "safe_safe"
      keyPassword "safesafe"
      storeFile file("debug.keystore")
      storePassword "safesafe"
    }
    if (useUploadKeystore) {
      final signingProps = new Properties()
      keystoreProperties.withInputStream {
        signingProps.load(it)
      }
      upload {
        keyAlias "safe_safe"
        keyPassword signingProps["keyPassword"]
        storeFile uploadKeystore
        storePassword signingProps["storePassword"]
      }
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }

    release {
      if (useUploadKeystore) {
        signingConfig signingConfigs.upload
      } else {
        signingConfig signingConfigs.debug
      }

      minifyEnabled true
      shrinkResources true
    }
  }

  packagingOptions {
    exclude "**/*.properties"
    exclude "*.properties"
    exclude "META-INF/*.version"
    exclude "META-INF/proguard/*"
    exclude "META-INF/*.properties"
    exclude "META-INF/androidx.*"
    exclude "META-INF/CHANGES"
    exclude "META-INF/CHANGES.md"
    exclude "META-INF/CHANGES.txt"
    exclude "META-INF/README"
    exclude "META-INF/README.md"
    exclude "META-INF/README.txt"
  }
}

dependencies {
  implementation "com.google.androidbrowserhelper:androidbrowserhelper:1.2.0"
}
